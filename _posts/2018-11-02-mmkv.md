---
layout: post
title:  "MMKV阅读笔记"
date:   2018-11-02
excerpt:  "本文是笔者阅读MMKV的笔记"
tag:
- iOS
comments: true
---

MMKV是一个key-value存储的库，下面笔者来分析下它的源代码。

# C函数

> MMKV中使用了一些C语言提供的内存和文件相关的函数，下面先列举出来：

## 文件相关函数

`int open(const char *, int, ...);`
将给定的文件路径以指定flags和指定mode打开，返回file descriptor。具体flags和mode的取值可以参看头文件注释。

`int ftruncate(int fd, off_t length);`
将open函数返回的file descriptor中指向的文件大小改为length指定的大小。如果原来文件大小大于length的话，则超过部分会被丢弃。

`int fstat(int, struct stat *buf);`
给定open函数返回的file descriptor，返回该文件描述符指向的文件的文件信息，将其存储在buf指针指向的结构体中。

`int stat(const char *file_name, struct stat *buf);`
给定文件名，返回该文件对应的文件信息，将其存储在buf指针指向的结构体中。

`int lstat(const char *file_name, struct stat *buf);`
和stat函数功能一样，只是当文件是符号链接时，lstat返回的是该符号链接的信息。

## 内存相关函数

`void *memcpy(void *__dst, const void *__src, size_t __n);`
将src地址开始的连续n个字节数据复制到以dst地址开始的空间内。

`void *memset(void *__b, int __c, size_t __len);`
将b地址开始的前len个字节全部设置为c。当b指向一段字符时，c也可以为char类型，实际设置的则是该字符对应的ASCII码值。

`void *mmap(void *start, size_t length, int prot, int flags, int fd, off_t offsize);`
将open函数返回的file descriptor中长为length的部分对应到以start地址（设置为nullptr系统会自动分配，然后返回）开始的内存区域中。实现操作内存映射到文件中，也就是对内存的读写也就是对文件的读写。offsize表示文件偏移量，一般设置位0表示从文件最开始部分开始映射。此时需要将flags设置为`MAP_SHARED`。

`int mlock(const void *addr, size_t len);`
将内存指定大小加锁

`int munlock(const void *addr, size_t len);`
将内存指定大小解锁

# Protocol Buffer

> MMKV使用PB来进行数据的序列化存储

PB是Google开源的一种通信协议，和XML的功能是类似的，PB使用`varint`来压缩数据，这样节约了存储空间，提高了传输速度。

## varint

- 使用最少的字节来保存一个数字，比如一个int32_t的value存1，原本需要4个字节，而实际这个数字只占用了一个字节，所以使用该种方式只需要占用一个字节就可以保存改数字了。

- 这种方式的每个字节的第一位有特殊含义，如果该位为1说明后续的字节是该数字的一部分；如果该位为0，说明此时的字节就是该数字的最后一个字节。

> 而且PB使用了小端的方式来存储，也就是第一个字节存储了数字的最低位。 大端则第一个字节存储了最高位。

举个例子，在PB的varint下，150二进制存储为`9601`，变换如下所示:

```
96 01 = 1001 0110  0000 0001
       → 000 0001  ++  001 0110 (去掉首位标志位，倒置字节顺序)
       → 10010110
       → 128 + 16 + 4 + 2 = 150
```

# 编码 & 解码

前面说到MMKV使用PB进行数据序列化，实际工程中使用下面两个类进行二进制编码和解码。

## MiniCodedOutputData

> 该类用于编码

```
uint8_t *m_ptr;
size_t m_size;
int32_t m_position;

MiniCodedOutputData::MiniCodedOutputData(void *ptr, size_t len)
    : m_ptr((uint8_t *) ptr), m_size(len), m_position(0) {
}

MiniCodedOutputData::MiniCodedOutputData(NSMutableData *oData)
    : m_ptr((uint8_t *) oData.mutableBytes), m_size(oData.length), m_position(0) {
}
```

上面是该类的属性和两个构造方法，用于初始化data指针和data长度和当前position。为接下来往data中写数据做好准备。

### write

write方法分为四类，varint，浮点型（float & double），string，data四种, 其中varint和浮点型内部都会调用`writeRawByte`来将一个字节的数据存进data中。

- byte

将一个字节的数据存储到data中很简单，首先判断位置有没有越界，然后一个简单的赋值操作：

```
void MiniCodedOutputData::writeRawByte(uint8_t value) {
	if (m_position == m_size) {
		NSString *reason = [NSString stringWithFormat:@"position: %d, bufferLength: %u", m_position, (unsigned int) m_size];
		@throw [NSException exceptionWithName:@"OutOfSpace" reason:reason userInfo:nil];
	}

	m_ptr[m_position++] = value;
}
```

- varint

write varint时分32位和64位，下面以32位为例来说明：

一个while循环：

- 1.首先判断这个value是不是不大于7位；
- 1.1 如果不大于7位，直接write一个字节，首字节为0，代表了这就是该value的最后一个字节；最后返回
- 1.2 如果大于7位，首页取value低位开始的七位，同时首位设置为1，代表该value后面还有字节；
- 1.2.1 将value右移7位，继续回到步骤1

```
void MiniCodedOutputData::writeRawVarint32(int32_t value) {
	while (YES) {
		if ((value & ~0x7f) == 0) {
			this->writeRawByte(value);
			return;
		} else {
			this->writeRawByte((value & 0x7f) | 0x80);
			value = logicalRightShift32(value, 7);
		}
	}
}
```

- 浮点型

write 浮点型没有使用varint，所以每个字节的8位都是用来存放数据的，下面以float来举例说明：

- 第一次取低位的8位，即一个字节的数据，write进data；
- 右移value8位，继续取低位的8位，即一个字节的数据，write进data；
- 右移value16位，继续取低位的8位，即一个字节的数据，write进data；
- 右移value24位，继续取低位的8位，即一个字节的数据，write进data；

此时32位数据已经全部write进data。

```
void MiniCodedOutputData::writeRawLittleEndian32(int32_t value) {
	this->writeRawByte((value) &0xff);
	this->writeRawByte((value >> 8) & 0xff);
	this->writeRawByte((value >> 16) & 0xff);
	this->writeRawByte((value >> 24) & 0xff);
}
```

- string

write string 相比之前的两种少了位运算和移位操作，不过多了一个存储string length的操作，然后调用NSString的`getBytes:maxLength:usedLength:encoding:options:range:remainingRange:`方法将string中的bytes数据存入我们给定的data的位置中。

```
void MiniCodedOutputData::writeString(NSString *value) {
	NSUInteger numberOfBytes = [value lengthOfBytesUsingEncoding:NSUTF8StringEncoding];
	this->writeRawVarint32((int32_t) numberOfBytes);
	[value getBytes:m_ptr + m_position
	         maxLength:numberOfBytes
	        usedLength:0
	          encoding:NSUTF8StringEncoding
	           options:0
	             range:NSMakeRange(0, value.length)
	    remainingRange:nullptr];
	m_position += numberOfBytes;
}
```

- data

write data 调用C函数`memcpy`将data中的bytes数据拷贝到我们MiniCodedOutputData中的data中。

```
void MiniCodedOutputData::writeRawData(NSData *value, int32_t offset, int32_t length) {
	if (length <= 0) {
		return;
	}
	if (m_size - m_position >= length) {
		memcpy(m_ptr + m_position, ((uint8_t *) value.bytes) + offset, length);
		m_position += length;
	} else {
		[NSException exceptionWithName:@"Space" reason:@"too much data than calc" userInfo:nil];
	}
}
```

## MiniCodedInputData

> 该类用于解码，

```
uint8_t *m_ptr;
int32_t m_size;
int32_t m_position;

MiniCodedInputData::MiniCodedInputData(NSData *oData)
    : m_ptr((uint8_t *) oData.bytes), m_size((int32_t) oData.length), m_position(0) {
}
```

上面是该类的三个属性和一个构造方法，和MiniCodedOutputData类似，一个区别是此时给`m_ptr`赋值的是data的`bytes`属性，通过该属性不能改变data中的数据。

### read

read方法同样分为四类，varint，浮点型（float & double），string，data四种, 其中varint和浮点型内部都会调用`readRawByte`来读取一个字节的数据。

- byte

判断position有没有越界，然后从data中取一个字节的数据返回。

```
int8_t MiniCodedInputData::readRawByte() {
	if (m_position == m_size) {
		NSString *reason = [NSString stringWithFormat:@"reach end, bufferPos: %d, bufferSize: %d", m_position, m_size];
		@throw [NSException exceptionWithName:@"InvalidProtocolBuffer" reason:reason userInfo:nil];
		return -1;
	}
	int8_t *bytes = (int8_t *) m_ptr;
	return bytes[m_position++];
}
```

- varint

read varint时分32位和64位

#### 64位 varint

初始化shift为0，result(`int64_t`)为0,一个while循环：

- 1.首先判断shift是否小于64
- 1.1 如果shift小于64,根据当前position读取一个字节的数据，将该字节的低7位左移shift位，放入result非0的低7位；
- 1.2 如果读取的该字节数据最高位为0，那么说明这个int后面没有其他字节，所以直接返回result；
- 1.3 shift加7，继续回到步骤1

```
int64_t MiniCodedInputData::readRawVarint64() {
	int32_t shift = 0;
	int64_t result = 0;
	while (shift < 64) {
		int8_t b = this->readRawByte();
		result |= (int64_t)(b & 0x7f) << shift;
		if ((b & 0x80) == 0) {
			return result;
		}
		shift += 7;
	}
	@throw [NSException exceptionWithName:@"InvalidProtocolBuffer" reason:@"malformedVarint" userInfo:nil];
	return -1;
}
```
#### 32位 varint

读取32位和64位类似，不过在读取32位int的时候，方法内部进行了位数判断，如果可能的话会丢弃高32位存储的字节数据。

```
int32_t MiniCodedInputData::readRawVarint32() {
	int8_t tmp = this->readRawByte();
	if (tmp >= 0) {
		return tmp;
	}
	int32_t result = tmp & 0x7f;
	if ((tmp = this->readRawByte()) >= 0) {
		result |= tmp << 7;
	} else {
		result |= (tmp & 0x7f) << 7;
		if ((tmp = this->readRawByte()) >= 0) {
			result |= tmp << 14;
		} else {
			result |= (tmp & 0x7f) << 14;
			if ((tmp = this->readRawByte()) >= 0) {
				result |= tmp << 21;
			} else {
				result |= (tmp & 0x7f) << 21;
				result |= (tmp = this->readRawByte()) << 28;
				if (tmp < 0) {
					// discard upper 32 bits.
					for (int i = 0; i < 5; i++) {
						if (this->readRawByte() >= 0) {
							return result;
						}
					}
					@throw [NSException exceptionWithName:@"InvalidProtocolBuffer" reason:@"malformedVarint" userInfo:nil];
					return -1;
				}
			}
		}
	}
	return result;
}
```

- 浮点型

write 浮点型没有使用varint，所以read 浮点型同样也不需要使用 read varint的方式，下面以float来说明：  

依次读取4个字节的数据，将其依次放入int32_t的32位中返回, 比read varint简单不少。

```
int32_t MiniCodedInputData::readRawLittleEndian32() {
	int8_t b1 = this->readRawByte();
	int8_t b2 = this->readRawByte();
	int8_t b3 = this->readRawByte();
	int8_t b4 = this->readRawByte();
	return (((int32_t) b1 & 0xff)) |
	       (((int32_t) b2 & 0xff) << 8) |
	       (((int32_t) b3 & 0xff) << 16) |
	       (((int32_t) b4 & 0xff) << 24);
}
```

- string

同样的read string没有位运算和移位操作，

记得之前我们write string的时候，是先write了string length，然后存储了string本身的bytes，所以这里read string的时候，首先调用`readRawVarint32`取到string的length，然后进行下面判断:

- 1. size > 0 && size <= (m_size - m_position),使用NSString的`initWithBytes:length:encoding`方法初始化string，position加上size，返回string。
- 2. size == 0, 返回空字符串。
- 3. size < 0, 返回nil。

```
NSString *MiniCodedInputData::readString() {
	int32_t size = this->readRawVarint32();
	if (size <= (m_size - m_position) && size > 0) {
		NSString *result = [[NSString alloc] initWithBytes:(m_ptr + m_position)
		                                            length:size
		                                          encoding:NSUTF8StringEncoding];
		m_position += size;
		return result;
	} else if (size == 0) {
		return @"";
	} else if (size < 0) {
		@throw [NSException exceptionWithName:@"InvalidProtocolBuffer" reason:@"negativeSize" userInfo:nil];
		return nil;
	} else {
		@throw [NSException exceptionWithName:@"InvalidProtocolBuffer" reason:@"truncatedMessage" userInfo:nil];
	}
	return nil;
}
```

- data

和read string一样，首先获取data的size，然后使用NSData的`dataWithBytes:length:`初始化result，position加上size，返回result。

```
NSData *MiniCodedInputData::readData() {
	int32_t size = this->readRawVarint32();
	if (size <= m_size - m_position && size > 0) {
		NSData *result = [NSData dataWithBytes:(m_ptr + m_position) length:size];
		m_position += size;
		return result;
	} else if (size < 0) {
		@throw [NSException exceptionWithName:@"InvalidProtocolBuffer" reason:@"negativeSize" userInfo:nil];
		return nil;
	}
	return nil;
}
```

## MiniPBCoder

这个类主要是用于object的编码和解码。

> 该类会用到`MiniPBEncodeItem`结构体, 结构体中属性如下所示：

```
MiniPBEncodeItemType type;
int32_t compiledSize; // object的size 加上 object size的length 的size
int32_t valueSize; // object的size
union {
    void *objectValue;
    void *tmpObjectValue; // this object should release on dealloc
} value;
```

### encode

`+ (NSData *)encodeDataWithObject:(id)obj;` 方法实现如下：

根据object构建`m_encodeItems`，这个属性类型为`std::vector<MiniPBEncodeItem> *`，用来存放解析object后的`MiniPBEncodeItem`结构体。具体实现在`- (size_t)prepareObjectForEncode:(NSObject *)obj` 方法中。    

根据`- (size_t)prepareObjectForEncode:(NSObject *)obj`返回的index，从`m_encodeItems`中取到`MiniPBEncodeItem`结构体，根据该结构的`compiledSize`创建`m_outputBuffer`, 用`m_outputBuffer` 创建`m_outputData`准备将数据写进`m_outputBuffer`中，这一步在`getEncodeData` 方法中。    

> 当object为字典时，需要返回第一个存储字典的encodeItem结构体的索引，因为该结构体中存放了这个字典正确的size, 所谓正确的size指的是字典所有key，value的`compiledSize`的和作为字典的`valueSize`，然后计算出字典的`compiledSize`。

最后一步，遍历`m_encodeItems`，根据`MiniPBEncodeItem` 的type 调用`MiniCodedOutputData`不同的write方法，将object中的数据写进`m_outputBuffer`中。 这一步在`writeRootObject`方法中。  

### decode

decode有两种，一种是非container类型(NSString, NSData, NSDate类型)，另一种就是container类型（NSDictionary)。

#### 非container类型

`+ (id)decodeObjectOfClass:(Class)cls fromData:(NSData *)oData;` 方法实现如下：

使用oData初始化`m_inputBuffer`, 用`m_inputBuffer`初始化`m_inputData`。   

调用`decodeOneObject:ofClass:`方法，该方法根据cls类型调用`MiniCodedInputData`不同的read方法，将read的结果直接返回。  


#### container类型

`+ (id)decodeContainerOfClass:(Class)cls withValueClass:(Class)valueClass fromData:(NSData *)oData;` 方法实现如下：

使用oData初始化`m_inputBuffer`, 用`m_inputBuffer`初始化`m_inputData`。     

判断cls类型，如果是字典类型，则调用`decodeOneDictionaryOfValueClass:` 方法解析字典中内容。     

将`m_isTopObject`改为NO，创建一个可变的字典，首先跳过存储字典size的字节，然后while循环，首先读key调用`MiniCodedInputData`的`readString`方法，然后读value依然调用`decodeOneObject:ofClass:`方法，判断value是否为nil，不为空则将key，value存到可变字典中，为空则从可变字典中移除该key对应的value。直到data遍历结束。   

# 接口

MMKV提供了一系列的set&get的方法，类似`NSUserDefaults`。

```
MMKV *mmkv = [MMKV defaultMMKV];
    
[mmkv setBool:YES forKey:@"bool"];
BOOL bValue = [mmkv getBoolForKey:@"bool"];
    
[mmkv setInt32:-1024 forKey:@"int32"];
int32_t iValue = [mmkv getInt32ForKey:@"int32"];
    
[mmkv setObject:@"hello, mmkv" forKey:@"string"];
NSString *str = [mmkv getObjectOfClass:NSString.class forKey:@"string"];
```

以上代码从MMKV Demo中摘录，我们可以发现使用起来和`NSUserDefaults`十分类似。

## set

MMKV提供给我们的各种类型的set方法，其实内部都是通过将不同类型的数据先转成二进制形式，然后内部再调用`setData:forKey:`完成存储。


# 文件布局

我们实际使用，来看看使用MMKV存储的文件格式。

```
MMKV *mmkv = [MMKV defaultMMKV];
int times = 7;
while (times > 0) {
    [mmkv setInt32:150 forKey:@"int"];
    times -= 1;
}
```

上面代码我们将int作为key，150作为value存储起来，我们打开沙盒里的文件，内容如下所示：

```
3200 0000 0703 696e 7402 9601 0369 6e74
0296 0103 696e 7402 9601 0369 6e74 0296
0103 696e 7402 9601 0369 6e74 0296 0103
696e 7402 9601 0000 0000 0000 0000 0000
0000 0000 0000 0000 0000 0000 0000 0000
0000 0000 0000 0000 0000 0000 0000 0000
0000 0000 0000 0000 0000 0000 0000 0000
0000 0000 0000 0000 0000 0000 0000 0000
.... 后面全部为0
```

- 前面4个字节其实就是`m_actualSize`
- 后面 `0703 696e 7402 9601`

 + `07` 标记了这个map的size
 + `03 696e 74` 表示key int的length(`03`)，和key int 的内容(`696e 74`)
 + `02 9601` 表示value 150的length(`02`)，和 valye 150的内容(`9601`)

# References

[https://developers.google.com/protocol-buffers/docs/encoding](https://developers.google.com/protocol-buffers/docs/encoding)

[https://github.com/Tencent/MMKV](https://github.com/Tencent/MMKV)
